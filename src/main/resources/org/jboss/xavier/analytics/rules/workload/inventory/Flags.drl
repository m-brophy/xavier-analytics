package org.jboss.xavier.analytics.rules.workload.inventory;

import org.jboss.xavier.analytics.pojo.input.workload.inventory.VMWorkloadInventoryModel
import org.jboss.xavier.analytics.pojo.output.workload.inventory.WorkloadInventoryReportModel
import java.util.HashSet;

dialect "java"
agenda-group "Flags"
lock-on-active true
auto-focus false

rule "Flag_Nics"
    when
        vmWorkloadInventoryModel : VMWorkloadInventoryModel(
            nicsCount != null,
            nicsCount > 4
        )
        workloadInventoryReport : WorkloadInventoryReportModel()
    then
        modify(workloadInventoryReport)
        {
            addFlagIMS(WorkloadInventoryReportModel.MORE_THAN_4_NICS_FLAG_NAME)
        }
end

rule "Flag_Rdm_Disk"
    when
        vmWorkloadInventoryModel : VMWorkloadInventoryModel(
            hasRdmDisk != null,
            hasRdmDisk == true
        )
        workloadInventoryReport : WorkloadInventoryReportModel()
    then
        modify(workloadInventoryReport)
        {
            addFlagIMS(WorkloadInventoryReportModel.RDM_DISK_FLAG_NAME)
        }
end


rule "Flag_Cpu_Memory_Hotplug_Memory_Add"
    when
        vmWorkloadInventoryModel : VMWorkloadInventoryModel(
            eval(hasMemoryHotAdd != null &&
            hasMemoryHotAdd.equals(true))
        )
        workloadInventoryReport : WorkloadInventoryReportModel()
    then
        modify(workloadInventoryReport)
        {
            addFlagIMS(WorkloadInventoryReportModel.CPU_MEMORY_HOTPLUG_FLAG_NAME)
        }
end


rule "Flag_Cpu_Memory_Hotplug_Cpu_Add"
    when
        vmWorkloadInventoryModel : VMWorkloadInventoryModel(
            eval(hasCpuHotAdd != null &&
            hasCpuHotAdd.equals(true))
        )
        workloadInventoryReport : WorkloadInventoryReportModel()
    then
        modify(workloadInventoryReport)
        {
            addFlagIMS(WorkloadInventoryReportModel.CPU_MEMORY_HOTPLUG_FLAG_NAME)
        }
end


rule "Flag_Cpu_Memory_Hotplug_Cpu_Remove"
    when
        vmWorkloadInventoryModel : VMWorkloadInventoryModel(
            eval(hasCpuHotRemove != null &&
            hasCpuHotRemove.equals(true))
        )
        workloadInventoryReport : WorkloadInventoryReportModel()
    then
        modify(workloadInventoryReport)
        {
            addFlagIMS(WorkloadInventoryReportModel.CPU_MEMORY_HOTPLUG_FLAG_NAME)
        }
end
        
rule "Flag_Cpu_Affinity"
    when
        vmWorkloadInventoryModel : VMWorkloadInventoryModel(
            cpuAffinityNotNull != null,
            cpuAffinityNotNull == true
        )
        workloadInventoryReport : WorkloadInventoryReportModel()
    then
        modify(workloadInventoryReport)
        {
            addFlagIMS(WorkloadInventoryReportModel.CPU_AFFINITY_FLAG_NAME)
        }
end


